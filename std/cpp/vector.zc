// std/cpp/vector.zc
// Zen-C native wrapper for std::vector<int> (requires --cpp)

include <vector>

struct CppVectorInt {
    ptr: void*
}

raw {
    #ifdef __cplusplus
    extern "C" {
        void* _z_cpp_vector_int_new() {
            return new std::vector<int>();
        }
        void _z_cpp_vector_int_delete(void* p) {
            delete static_cast<std::vector<int>*>(p);
        }
        void _z_cpp_vector_int_push_back(void* p, int v) {
            static_cast<std::vector<int>*>(p)->push_back(v);
        }
        int _z_cpp_vector_int_get(void* p, int index) {
            return (*static_cast<std::vector<int>*>(p))[index];
        }
        int _z_cpp_vector_int_size(void* p) {
            return static_cast<int>(static_cast<std::vector<int>*>(p)->size());
        }
    }
    #endif
}

extern fn _z_cpp_vector_int_new() -> void*;
extern fn _z_cpp_vector_int_delete(p: void*);
extern fn _z_cpp_vector_int_push_back(p: void*, v: int);
extern fn _z_cpp_vector_int_get(p: void*, index: int) -> int;
extern fn _z_cpp_vector_int_size(p: void*) -> int;

fn cpp_vector_int_create() -> CppVectorInt {
    return CppVectorInt { ptr: _z_cpp_vector_int_new() };
}

impl CppVectorInt {
    fn push(self, v: int) {
        _z_cpp_vector_int_push_back(self.ptr, v);
    }
    fn get(self, index: int) -> int {
        return _z_cpp_vector_int_get(self.ptr, index);
    }
    fn len(self) -> int {
        return _z_cpp_vector_int_size(self.ptr);
    }
}

impl Drop for CppVectorInt {
    fn drop(self) {
        if self.ptr != null {
            _z_cpp_vector_int_delete(self.ptr);
            self.ptr = null;
        }
    }
}
